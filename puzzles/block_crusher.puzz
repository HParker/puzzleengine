title Simple Block Crushing Game
author Adam Hess
homepage www.puzzlescript.net

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
Orange Yellow
00000
0...0
0...0
0...0
00000

FlatCrate
Orange Yellow
00000
01000
00100
00010
00000

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target


=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target, FlatCrate
Player, Wall, Crate

======
RULES
======

[ action  Player | Crate ] -> [ Player | FlatCrate  ]

==============
WINCONDITIONS
==============

All Player on Target
No Crate

=======
LEVELS
=======

message crush the boxes and get out

####..
#.O#..
#**###
#.P..#
#..*.#
#..###
####..



#######
#.P...#
#.....#
#.***.#
#.*O*.#
#.***.#
#.....#
#######
